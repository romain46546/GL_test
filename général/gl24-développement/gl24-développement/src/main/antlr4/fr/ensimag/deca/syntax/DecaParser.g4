parser grammar DecaParser;

options {
    // Default language but name it anyway
    //
    language  = Java;

    // Use a superclass to implement all helper
    // methods, instance variables and overrides
    // of ANTLR default methods, such as error
    // handling.
    //
    superClass = AbstractDecaParser;

    // Use the vocabulary generated by the accompanying
    // lexer. Maven knows how to work out the relationship
    // between the lexer and parser and will build the
    // lexer before the parser. It will also rebuild the
    // parser if the lexer changes.
    //
    tokenVocab = DecaLexer;

}

// which packages should be imported?
@header {
    import fr.ensimag.deca.tree.*;
    import java.io.PrintStream;
}

@members {
    @Override
    protected AbstractProgram parseProgram() {
        return prog().tree;
    }
}

prog returns[AbstractProgram tree]
    : b=block EOF {
            ListDeclClass listClass = new ListDeclClass();
            assert($b.decls != null);
            assert($b.insts != null);
            Main m = new Main($b.decls, $b.insts);
            setLocation(m, $b.start);
            $tree = new Program(listClass, m);
            setLocation($tree, $b.start);
        }
    ;

block returns[ListDeclVar decls, ListInst insts]
    : OBRACE list_decl list_inst CBRACE {
            assert($list_decl.tree != null);
            assert($list_inst.tree != null);
            $decls = $list_decl.tree;
            $insts = $list_inst.tree;
        }
    ;

list_decl returns[ListDeclVar tree]
@init   {
            $tree = new ListDeclVar();
        }
    : decl_var_set[$tree]*
    ;

decl_var_set[ListDeclVar l]
    : type list_decl_var[$l,$type.tree] SEMI
    ;

list_decl_var[ListDeclVar l, AbstractIdentifier t]
    : dv1=decl_var[$t] {
        $l.add($dv1.tree);
        } (COMMA dv2=decl_var[$t] {
        $l.add($dv2.tree);
        }
      )*
    ;

decl_var[AbstractIdentifier t] returns[AbstractDeclVar tree]
@init   {
            AbstractInitialization init = new NoInitialization();
        }
    : i=ident {
        }
      (EQUALS e=expr {
            assert($e.tree != null);
            init = new Initialization($e.tree);
                setLocation(init, $e.start);
        }
      )? {
            assert($i.tree != null);
            $tree = new DeclVar($t, $i.tree, init);
            setLocation($tree, $i.start);
        }
    ;

list_inst returns[ListInst tree]
@init {
            $tree = new ListInst();
        }
    : (i=inst {
            assert($i.tree != null);
            $tree.add($i.tree);
        }
      | b=block {
            assert($b.insts != null);
            for (AbstractInst inst : $b.insts.getList()) {
                $tree.add(inst);
            }
        }
      )*
    ;

inst returns[AbstractInst tree]
    : e1=assign_expr SEMI {
            assert($e1.tree != null);
            if (! ($e1.tree instanceof Assign)) {
                throw new DecaRecognitionException(this, $ctx) {
                    private static final long serialVersionUID = 1L;
                    @Override
                    public String getMessage() {
                        return "Invalid instruction";
                    }
                };
            }
            $tree = $e1.tree;
        }
    | s=SEMI {
            $tree = new NoOperation();
            setLocation($tree, $s);
        }
    | p=PRINT OPARENT list_expr CPARENT SEMI {
            assert($list_expr.tree != null);
            $tree = new Print(false, $list_expr.tree);
            setLocation($tree, $p);
        }
    | pl=PRINTLN OPARENT list_expr CPARENT SEMI {
            assert($list_expr.tree != null);
            $tree = new Println(false, $list_expr.tree);
            setLocation($tree, $pl);
        }
    | px=PRINTX OPARENT list_expr CPARENT SEMI {
            assert($list_expr.tree != null);
            $tree = new Print(true, $list_expr.tree);
            setLocation($tree, $px);
        }
    | plx=PRINTLNX OPARENT list_expr CPARENT SEMI {
            assert($list_expr.tree != null);
            $tree = new Println(true, $list_expr.tree);
            setLocation($tree, $plx);
        }
    | if_then_else {
            assert($if_then_else.tree != null);
            $tree = $if_then_else.tree;
        }
    | w=WHILE OPARENT condition=expr CPARENT OBRACE body=list_inst CBRACE {
            assert($condition.tree != null);
            assert($body.tree != null);
            $tree = new While($condition.tree, $body.tree);
            setLocation($tree, $w);
        }
// STAGE_B_TODO | RETURN expr SEMI {
// STAGE_B_TODO         assert($expr.tree != null);
// STAGE_B_TODO     }
    ;

if_then_else returns[IfThenElse tree]
@init {
            ListInst currentElse = null;
        }
    : if1=IF OPARENT condition=expr CPARENT OBRACE li_if=list_inst CBRACE {
            assert($condition.tree != null);
            assert($li_if.tree != null);
            ListInst elseList = new ListInst();
            $tree = new IfThenElse($condition.tree, $li_if.tree, elseList);
            setLocation($tree, $if1);
            currentElse = elseList;
        }
      (ELSE elsif=IF OPARENT elsif_cond=expr CPARENT OBRACE elsif_li=list_inst CBRACE {
            assert($elsif_cond.tree != null);
            assert($elsif_li.tree != null);
            ListInst nestedElse = new ListInst();
            IfThenElse nestedIf = new IfThenElse($elsif_cond.tree, $elsif_li.tree, nestedElse);
            setLocation(nestedIf, $elsif);
            currentElse.add(nestedIf);
            currentElse = nestedElse;
        }
      )*
      (ELSE OBRACE li_else=list_inst CBRACE {
            assert($li_else.tree != null);
            for (AbstractInst inst : $li_else.tree.getList()) {
                currentElse.add(inst);
            }
        }
      )?
    ;

list_expr returns[ListExpr tree]
@init   {
            $tree = new ListExpr();
        }
    : (e1=expr {
            assert($e1.tree != null);
            $tree.add($e1.tree);
        }
       (COMMA e2=expr {
            assert($e2.tree != null);
            $tree.add($e2.tree);
        }
       )* )?
    ;

expr returns[AbstractExpr tree]
    : assign_expr {
            assert($assign_expr.tree != null);
            $tree = $assign_expr.tree;
        }
    ;

assign_expr returns[AbstractExpr tree]
    : e=or_expr (
        /* condition: expression e must be a "LVALUE" */ {
            if (! ($e.tree instanceof AbstractLValue)) {
                throw new InvalidLValue(this, $ctx);
            }
        }
        eq=EQUALS e2=assign_expr {
            assert($e.tree != null);
            assert($e2.tree != null);
            $tree = new Assign((AbstractLValue)$e.tree, $e2.tree);
            setLocation($tree, $eq);
        }
      | /* epsilon */ {
            assert($e.tree != null);
            $tree = $e.tree;
        }
      )
    ;

or_expr returns[AbstractExpr tree]
    : e=and_expr {
            assert($e.tree != null);
            $tree = $e.tree;
        }
    | e1=or_expr o=OR e2=and_expr {
            assert($e1.tree != null);
            assert($e2.tree != null);
            $tree = new Or($e1.tree, $e2.tree);
            setLocation($tree, $o);
       }
    ;

and_expr returns[AbstractExpr tree]
    : e=eq_neq_expr {
            assert($e.tree != null);
            $tree = $e.tree;
        }
    |  e1=and_expr a=AND e2=eq_neq_expr {
            assert($e1.tree != null);                         
            assert($e2.tree != null);
            $tree = new And($e1.tree, $e2.tree);
            setLocation($tree, $a);
        }
    ;

eq_neq_expr returns[AbstractExpr tree]
    : e=inequality_expr {
            assert($e.tree != null);
            $tree = $e.tree;
        }
    | e1=eq_neq_expr eq=EQEQ e2=inequality_expr {
            assert($e1.tree != null);
            assert($e2.tree != null);
            $tree = new Equals($e1.tree, $e2.tree);
            setLocation($tree, $eq);
        }
    | e1=eq_neq_expr neq=NEQ e2=inequality_expr {
            assert($e1.tree != null);
            assert($e2.tree != null);
            $tree = new NotEquals($e1.tree, $e2.tree);
            setLocation($tree, $neq);
        }
    ;

inequality_expr returns[AbstractExpr tree]
    : e=sum_expr {
            assert($e.tree != null);
            $tree = $e.tree;
        }
    | e1=inequality_expr le=LEQ e2=sum_expr {
            assert($e1.tree != null);
            assert($e2.tree != null);
            $tree = new LowerOrEqual($e1.tree, $e2.tree);
            setLocation($tree, $le);
        }
    | e1=inequality_expr ge=GEQ e2=sum_expr {
            assert($e1.tree != null);
            assert($e2.tree != null);
            $tree = new GreaterOrEqual($e1.tree, $e2.tree);
            setLocation($tree, $ge);
        }
    | e1=inequality_expr gt=GT e2=sum_expr {
            assert($e1.tree != null);
            assert($e2.tree != null);
            $tree = new Greater($e1.tree, $e2.tree);
            setLocation($tree, $gt);
        }
    | e1=inequality_expr lt=LT e2=sum_expr {
            assert($e1.tree != null);
            assert($e2.tree != null);
            $tree = new Lower($e1.tree, $e2.tree);
            setLocation($tree, $lt);
        }
// STAGE_B_TODO | e1=inequality_expr INSTANCEOF type {
// STAGE_B_TODO         assert($e1.tree != null);
// STAGE_B_TODO         assert($type.tree != null);
// STAGE_B_TODO     }
    ;


sum_expr returns[AbstractExpr tree]
    : e=mult_expr {
            assert($e.tree != null);
            $tree = $e.tree;
        }
    | e1=sum_expr p=PLUS e2=mult_expr {
            assert($e1.tree != null);
            assert($e2.tree != null);
            $tree = new Plus($e1.tree, $e2.tree);
            setLocation($tree, $p);
        }
    | e1=sum_expr m=MINUS e2=mult_expr {
            assert($e1.tree != null);
            assert($e2.tree != null);
            $tree = new Minus($e1.tree, $e2.tree);
            setLocation($tree, $m);
        }
    ;

mult_expr returns[AbstractExpr tree]
    : e=unary_expr {
            assert($e.tree != null);
            $tree = $e.tree;
        }
    | e1=mult_expr t=TIMES e2=unary_expr {
            assert($e1.tree != null);                                         
            assert($e2.tree != null);
            $tree = new Multiply($e1.tree, $e2.tree);
            setLocation($tree, $t);
        }
    | e1=mult_expr s=SLASH e2=unary_expr {
            assert($e1.tree != null);                                         
            assert($e2.tree != null);
            $tree = new Divide($e1.tree, $e2.tree);
            setLocation($tree, $s);
        }
    | e1=mult_expr p=PERCENT e2=unary_expr {
            assert($e1.tree != null);                                                                          
            assert($e2.tree != null);
            $tree = new Modulo($e1.tree, $e2.tree);
            setLocation($tree, $p);
        }
    ;

unary_expr returns[AbstractExpr tree]
    : op=MINUS e=unary_expr {
            assert($e.tree != null);
            $tree = new UnaryMinus($e.tree);
            setLocation($tree, $op);
        }
    | op=EXCLAM e=unary_expr {
            assert($e.tree != null);
            $tree = new Not($e.tree);
            setLocation($tree, $op);
        }
    | select_expr {
            assert($select_expr.tree != null);
            $tree = $select_expr.tree;
        }
    ;

select_expr returns[AbstractExpr tree]
    : e=primary_expr {
            assert($e.tree != null);
            $tree = $e.tree;
        }
// STAGE_B_TODO | e1=select_expr DOT i=ident {
// STAGE_B_TODO         assert($e1.tree != null);
// STAGE_B_TODO         assert($i.tree != null);
// STAGE_B_TODO     }
// STAGE_B_TODO     (o=OPARENT args=list_expr CPARENT {
// STAGE_B_TODO         // we matched "e1.i(args)"
// STAGE_B_TODO         assert($args.tree != null);
// STAGE_B_TODO     }
// STAGE_B_TODO     | /* epsilon */ {
// STAGE_B_TODO         // we matched "e.i"
// STAGE_B_TODO     }
// STAGE_B_TODO     )
    ;

primary_expr returns[AbstractExpr tree]
    : ident {
            assert($ident.tree != null);
            $tree = $ident.tree;
        }
// STAGE_B_TODO | m=ident OPARENT args=list_expr CPARENT {
// STAGE_B_TODO         assert($args.tree != null);
// STAGE_B_TODO         assert($m.tree != null);
// STAGE_B_TODO     }
    | OPARENT expr CPARENT {
            assert($expr.tree != null);
            $tree = $expr.tree;
        }
    | r=READINT OPARENT CPARENT {
            $tree = new ReadInt();
            setLocation($tree, $r);
        }
    | f=READFLOAT OPARENT CPARENT {
            $tree = new ReadFloat();
            setLocation($tree, $f);
        }
// STAGE_B_TODO | NEW ident OPARENT CPARENT {
// STAGE_B_TODO         assert($ident.tree != null);
// STAGE_B_TODO     }
// STAGE_B_TODO | cast=OPARENT type CPARENT OPARENT expr CPARENT {
// STAGE_B_TODO         assert($type.tree != null);
// STAGE_B_TODO         assert($expr.tree != null);
// STAGE_B_TODO     }
    | literal {
            assert($literal.tree != null);
            $tree = $literal.tree;
        }
    ;

type returns[AbstractIdentifier tree]
    : t=KW_INT {
            fr.ensimag.deca.tools.SymbolTable.Symbol sym = getDecacCompiler().createSymbol("int");
            $tree = new Identifier(sym);
            setLocation($tree, $t);
        }
    | t=KW_FLOAT {
            fr.ensimag.deca.tools.SymbolTable.Symbol sym = getDecacCompiler().createSymbol("float");
            $tree = new Identifier(sym);
            setLocation($tree, $t);
        }
    | t=KW_BOOLEAN {
            fr.ensimag.deca.tools.SymbolTable.Symbol sym = getDecacCompiler().createSymbol("boolean");
            $tree = new Identifier(sym);
            setLocation($tree, $t);
        }
    ;

literal returns[AbstractExpr tree]
    : i=INT {
            $tree = new IntLiteral(Integer.parseInt($i.text));
            setLocation($tree, $i);
        }
    | fd=FLOAT {
            $tree = new FloatLiteral(Float.parseFloat($fd.text));
            setLocation($tree, $fd);
        }
    | s=STRING {
            String txt = $s.text.substring(1, $s.text.length() - 1);
            txt = txt.replace("\\\\", "\\").replace("\\\"", "\"");
            $tree = new StringLiteral(txt);
            setLocation($tree, $s);
        }
    | t=TRUE {
            $tree = new BooleanLiteral(true);
            setLocation($tree, $t);
        }
    | f=FALSE {
            $tree = new BooleanLiteral(false);
            setLocation($tree, $f);
        }
// STAGE_B_TODO | THIS {
// STAGE_B_TODO     }
// STAGE_B_TODO | NULL {
// STAGE_B_TODO     }
    ;

ident returns[AbstractIdentifier tree]
    : i=IDENT {
            fr.ensimag.deca.tools.SymbolTable.Symbol sym = getDecacCompiler().createSymbol($i.text);
            $tree = new Identifier(sym);
            setLocation($tree, $i);
        }
    ;

/****     Class related rules     ****/

list_classes returns[ListDeclClass tree]
@init   {
            $tree = new ListDeclClass();
        }
    :
      (c1=class_decl {
        }
      )*
    ;

class_decl
    : CLASS name=ident superclass=class_extension OBRACE class_body CBRACE {
        }
    ;

class_extension returns[AbstractIdentifier tree]
    : EXTENDS ident {
        }
    | /* epsilon */ {
        }
    ;

class_body
    : (m=decl_method {
        }
      | decl_field_set
      )*
    ;

decl_field_set
    : v=visibility t=type list_decl_field
      SEMI
    ;

visibility
    : /* epsilon */ {
        }
    | PROTECTED {
        }
    ;

list_decl_field
    : dv1=decl_field
        (COMMA dv2=decl_field
      )*
    ;

decl_field
    : i=ident {
        }
      (EQUALS e=expr {
        }
      )? {
        }
    ;

decl_method
@init {
}
    : type ident OPARENT params=list_params CPARENT (block {
        }
      | ASM OPARENT code=multi_line_string CPARENT SEMI {
        }
      ) {
        }
    ;

list_params
    : (p1=param {
        } (COMMA p2=param {
        }
      )*)?
    ;
    
multi_line_string returns[String text, Location location]
    : s=STRING {
            $text = $s.text;
            $location = tokenLocation($s);
        }
    | s=MULTI_LINE_STRING {
            $text = $s.text;
            $location = tokenLocation($s);
        }
    ;

param
    : type ident {
        }
    ;
